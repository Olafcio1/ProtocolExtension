import com.modrinth.minotaur.TaskModrinthUpload
import java.nio.file.Path
import java.nio.file.Files
import java.nio.file.StandardCopyOption

plugins {
    id 'fabric-loom' version '1.13.6'
    id 'maven-publish'
    id 'com.modrinth.minotaur' version "2.+"
}

version = project.mod_version
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

fabricApi {
    configureDataGeneration {
        client = true
    }
}

repositories {
    maven {
        name = 'papermc'
        url = 'https://repo.papermc.io/repository/maven-public/'
    }

    maven {
        url = 'https://repo.codemc.io/repository/maven-releases/'
    }

    maven {
        url = 'https://repo.codemc.io/repository/maven-snapshots/'
    }

    maven {
        url 'https://jitpack.io'
    }
}

dependencies {
    // Fabric
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"

    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    // Paper
    compileOnly "io.papermc.paper:paper-api:${project.minecraft_version}-${project.paper_version}"
    compileOnly "com.github.retrooper:packetevents-spigot:2.10.1"

    // Dependencies
    implementation 'com.github.MatrixCreations:MatrixColorAPI:v1.0.7'
    implementation 'net.bytebuddy:byte-buddy:1.18.2'
}

void expandFile(FileCopyDetails it) {
    it.expand "version": project.version,
              "minecraft_version": project.minecraft_version,
              "loader_version": project.loader_version
}

processResources {
    inputs.property "version", project.version
    inputs.property "minecraft_version", project.minecraft_version
    inputs.property "loader_version", project.loader_version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expandFile(it)
    }

    filesMatching("paper-plugin.yml") {
        expandFile(it)
    }
}

def targetJavaVersion = 21
tasks.withType(JavaCompile).configureEach {
    // ensure that the encoding is set to UTF-8, no matter what the system default is
    // this fixes some edge cases with special characters not displaying correctly
    // see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
    // If Javadoc is generated, this must be specified in that task too.
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible())
        it.options.release.set(targetJavaVersion)
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion)
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)

    withSourcesJar()
}

def bin = Path.of("$projectDir/bin")
tasks.named("jar") {
    doFirst {
        def binFiles = Files.list(bin)
        for (def bf : binFiles)
            Files.delete(bin.resolve(bf))
    }
}

tasks.named("remapJar") {
    doLast {
        Files.copy(
                Path.of("$projectDir/build/libs/" + remapJar.archiveFileName.get()),
                bin.resolve("protocolextension-${version}.jar"),
                StandardCopyOption.REPLACE_EXISTING
        )
    }
}

tasks.named("remapSourcesJar") {
    doLast {
        Files.move(
                Path.of("$projectDir/build/libs/" + remapSourcesJar.archiveFileName.get()),
                bin.resolve("protocolextension-${version}-api.jar"),
                StandardCopyOption.REPLACE_EXISTING,
                StandardCopyOption.ATOMIC_MOVE
        )
    }
}

tasks.named("build") {
    doLast {
        new ProcessBuilder()
                .command("git", "add", "-f", "${projectDir.path}/bin")
                .inheritIO()
                .start()
        .waitFor()
    }
}

private void nextVersion() {
    // Read the file
    def file = file("gradle.properties")
    def data = new String(file.newDataInputStream().readAllBytes())

    // Update it
    def newVersion = Integer.parseInt((String) version) + 1
    data = data.replace(
            "mod_version=" + version,
            "mod_version=" + newVersion
    )

    // Write it
    def writer = file.newWriter()
    writer.write(data)
    writer.flush()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

private static String gitChangelog() {
    return new ProcessBuilder()
            .command("git", "log", "-1", "--pretty=%B")
            .start()
    .inputStream.getText("UTF-8")
}

def m2 = tasks.register("Modrinth", TaskModrinthUpload) {
    it.dependsOn(tasks.named("modrinth"), remapJar)
    it.doFirst {
        modrinth {
            loaders = ["paper"]
            dependencies {
                required.project "packetevents"
            }
        }
    }

    it.doLast {
        nextVersion()

        new ProcessBuilder()
                .command("git", "add", "${projectDir.path}/gradle.properties")
                .start()
        .waitFor()

        new ProcessBuilder()
                .command("git", "commit", "--amend", "--no-edit")
                .start()
        .waitFor()

        new ProcessBuilder()
                .command("git", "push", "--force-with-lease")
                .start()
        .waitFor()
    }
}

tasks.modrinth {
    finalizedBy(m2)
}

modrinth {
    token = System.getenv("MODRINTH_TOKEN")
    projectId = "protocolextension"
    versionNumber = "${version}"
    versionType = "release"
    versionName = "ProtocolExtension stable-${version}-ml"
    changelog = gitChangelog()
    uploadFile = tasks.named("remapJar").flatMap { it.archiveFile }
    gameVersions = [minecraft_version].toList()

    loaders = ["fabric"]
    dependencies {
        required.project "fabric-api"
    }
}

// configure the maven publication
publishing {
    publications {
        create("mavenJava", MavenPublication) {
            artifactId = project.archives_base_name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
        // Notice: This block does NOT have the same function as the block in the top level.
        // The repositories here will be used for publishing your artifact, not for
        // retrieving dependencies.
    }
}

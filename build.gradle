import java.nio.file.CopyOption
import java.nio.file.Path
import java.nio.file.Files
import java.nio.file.StandardCopyOption

plugins {
    id 'fabric-loom' version '1.14-SNAPSHOT'
    id 'maven-publish'
    id 'me.modmuss50.mod-publish-plugin' version '1.1.0'
}

def generalDir = project.projectDir.getAbsolutePath().splitWithDelimiters("^.+?[/\\\\]IdeaProjects[/\\\\].+?[/\\\\]", 2)[1]
def generalPath = Path.of(generalDir)

apply from: "${generalDir}/versions.gradle"

version = "${property('mod_version')}+${sc.current.version}"
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

fabricApi {
    configureDataGeneration {
        client = true
    }
}

repositories {
    maven {
        name = 'papermc'
        url = 'https://repo.papermc.io/repository/maven-public/'
    }

    maven { url = 'https://repo.codemc.io/repository/maven-releases/' }
    maven { url = 'https://repo.codemc.io/repository/maven-snapshots/' }
    maven { url = 'https://jitpack.io' }
}

final def versions = (LinkedHashMap<
            String,
            LinkedHashMap<String, String>
>) ext.get("versions"),
          versionE = versions.get(sc.current.version)

dependencies {
    // Fabric
    minecraft "com.mojang:minecraft:${sc.current.version}"
    mappings "net.fabricmc:yarn:${sc.current.version}+build.${versionE.get("yarn")}:v2"

    modImplementation "net.fabricmc:fabric-loader:${property('fabric_loader')}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${versionE.get("fabric")}+${sc.current.version}"

    // Paper
    if (versionE.containsKey("paper")) {
        compileOnly "io.papermc.paper:paper-api:${sc.current.version}-${versionE.get("paper")}"
    } else if (versionE.containsKey("paper_copy")) {
        def copy = versionE.get("paper_copy")
        compileOnly "io.papermc.paper:paper-api:${copy}-${versions.get(copy).get("paper")}"
    }

    compileOnly "com.github.retrooper:packetevents-spigot:2.11.1"

    // Dependencies
    implementation 'net.bytebuddy:byte-buddy:1.18.2'
    include 'net.bytebuddy:byte-buddy:1.18.2'
}

void expandFile(FileCopyDetails it) {
    it.expand (
            "version": project.version,
            "minecraft_version": sc.current.version,
            "loader_version": property('fabric_loader')
    )
}

processResources {
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expandFile(it)
    }

    filesMatching("paper-plugin.yml") {
        expandFile(it)
    }
}

def targetJavaVersion = 21
tasks.withType(JavaCompile).configureEach {
    // ensure that the encoding is set to UTF-8, no matter what the system default is
    // this fixes some edge cases with special characters not displaying correctly
    // see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
    // If Javadoc is generated, this must be specified in that task too.
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible())
        it.options.release.set(targetJavaVersion)
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion)
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)

    withSourcesJar()
}

def bin = Path.of("$projectDir/bin")
tasks.named("jar") {
    doFirst {
        if (!Files.exists(bin)) {
            Files.createDirectory(bin)
            return
        }

        def binFiles = Files.list(bin)
        for (def bf : binFiles)
            Files.delete(bin.resolve(bf))
    }
}

tasks.named("remapJar") {
    doLast {
        var jar = Path.of("$projectDir/build/libs/" + remapJar.archiveFileName.get())
        if (!Files.exists(jar)) {
            return
        }

        Files.copy(
                jar,
                bin.resolve("protocolextension-${version}.jar"),
                (CopyOption) StandardCopyOption.REPLACE_EXISTING
        )
    }
}

tasks.named("remapSourcesJar") {
    doLast {
        Files.move(
                Path.of("$projectDir/build/libs/" + remapSourcesJar.archiveFileName.get()),
                bin.resolve("protocolextension-${version}-api.jar"),
                StandardCopyOption.REPLACE_EXISTING,
                StandardCopyOption.ATOMIC_MOVE
        )
    }
}

tasks.named("build") {
    doLast {
        new ProcessBuilder()
                .command("git", "add", "-f", "${projectDir.path}/bin")
                .inheritIO()
                .start()
        .waitFor()
    }
}

private void nextVersion() {
    // Read the file
    def file = file("gradle.properties")
    def data = new String(file.newDataInputStream().readAllBytes())

    // Update it
    def newVersion = Integer.parseInt((String) version) + 1
    data = data.replace(
            "mod_version=" + version,
            "mod_version=" + newVersion
    )

    // Write it
    def writer = file.newWriter()
    writer.write(data)
    writer.flush()
}

var updateVersion = tasks.register("updateVersion") {
    doFirst {
        new ProcessBuilder()
                .command("git", "reset")
                .directory(projectDir)
                .inheritIO()
        .start().waitFor()

        new ProcessBuilder()
                .command("git", "add", "gradle.properties")
                .directory(projectDir)
                .inheritIO()
        .start().waitFor()

        new ProcessBuilder()
                .command("git", "commit", "-m", "Version bump")
                .directory(projectDir)
                .inheritIO()
        .start().waitFor()

        new ProcessBuilder()
                .command("git", "stash")
                .directory(projectDir)
                .inheritIO()
        .start().waitFor()

        new ProcessBuilder()
                .command("git", "push")
                .directory(projectDir)
                .inheritIO()
        .start().waitFor()

        new ProcessBuilder()
                .command("git", "stash", "pop")
                .inheritIO()
        .start().waitFor()
    }
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archives_base_name}" }
    }
}

private static String gitChangelog() {
    return new ProcessBuilder()
            .command("git", "log", "-1", "--pretty=%B")
            .start()
    .inputStream.getText("UTF-8")
}

publishMods {
    file = tasks.named("remapJar").flatMap { it.archiveFile }
    changelog = gitChangelog()
    type = STABLE

    def options = modrinthOptions {
        accessToken = providers.environmentVariable("MODRINTH_TOKEN")
        projectId = "qhe9Yypz"
        displayName = "ProtocolExtension stable-${property('mod_version')}-ml"

        minecraftVersions.add(sc.current.version)
    }

    modrinth("modrinthFabric") {
        from options
        modLoaders.add("fabric")
        requires("fabric-api")
    }

    modrinth("modrinthPaper") {
        from options
        modLoaders.add("paper")
        requires("packetevents")
    }
}

tasks.publishMods {
    dependsOn(remapJar)
}

tasks.register("projectExpand") {
    doFirst {
        Files.copy(
                generalPath.resolve("./docs/CODE_OF_CONDUCT.md"),
                generalPath.resolve("./CODE_OF_CONDUCT.md"),
                StandardCopyOption.REPLACE_EXISTING
        )

        var readme = Files.readString(generalPath.resolve("./docs/README.template.md"))

        var vct = ""
        var vb = new StringBuilder()

        for (var version : versions.keySet()) {
            vb.append("| ${version} ")
            vb.append("| Supported ")
            vb.append("| [./versions/${version}/bin/${archives_base_name}]")
              .append(  "(https://github.com/Olafcio1/ProtocolExtension/raw/refs/heads/main/versions/${version}/bin/${archives_base_name}-${mod_version}+${version}.jar) |")
            vb.append("\n")

            vct += vb.toString()
            vb.setLength(0)
        }

        readme = readme.replace("(( version compatibility table ))", vct)
        Files.writeString(generalPath.resolve("./README.md"), readme)
    }
}

// configure the maven publication
publishing {
    publications {
        create("mavenJava", MavenPublication) {
            artifactId = project.archives_base_name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
        // Notice: This block does NOT have the same function as the block in the top level.
        // The repositories here will be used for publishing your artifact, not for
        // retrieving dependencies.
    }
}

//if (project.plugins.hasPlugin("dev.kikugie.stonecutter")) {
//    stonecutter.parameters {
//        swaps.put("minecraft", "\"${current.version}\"")
//        dependencies.put("fapi", node.project.property("fabric_version").toString())
//    }
//}
